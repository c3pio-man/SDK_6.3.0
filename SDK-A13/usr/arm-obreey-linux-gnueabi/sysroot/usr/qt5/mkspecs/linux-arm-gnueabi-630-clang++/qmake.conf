#
# qmake configuration for linux-g++
#

# arm-obreey_static-linux-gnueabi
MAKEFILE_GENERATOR      = UNIX
CONFIG                 += incremental
QMAKE_INCREMENTAL_STYLE = sublib

include(../common/linux.conf)
include(../common/gcc-base-unix.conf)
include(../common/clang.conf)

QMAKE_CFLAGS_RELEASE    = -O2 -fno-strict-aliasing
QMAKE_CFLAGS_DEBUG      = -O0 -g

QMAKE_CFLAGS            = -mcpu=cortex-a8 -march=armv7-a -mfpu=neon -mfloat-abi=softfp -fPIC -fno-omit-frame-pointer
QMAKE_CFLAGS_SHLIB      = -fPIC 
QMAKE_CFLAGS_YACC       = -Wno-unused -Wno-parentheses
QMAKE_CFLAGS_THREAD     = -D_REENTRANT
QMAKE_CFLAGS_HIDESYMS   = -fvisibility=hidden
QMAKE_LFLAGS_SHLIB      = -shared
QMAKE_LFLAGS_PLUGIN     = -shared

QMAKE_CXXFLAGS          = $$QMAKE_CFLAGS -DQT_NO_QWS_TRANSFORMED
QMAKE_CXXFLAGS_RELEASE += $$QMAKE_CFLAGS_RELEASE
QMAKE_CXXFLAGS_DEBUG   += $$QMAKE_CFLAGS_DEBUG
QMAKE_CXXFLAGS_SHLIB	= $$QMAKE_CFLAGS_SHLIB
QMAKE_CXXFLAGS_YACC     = $$QMAKE_CFLAGS_YACC
QMAKE_CXXFLAGS_THREAD	= $$QMAKE_CFLAGS_THREAD
QMAKE_CXXFLAGS_HIDESYMS = $$QMAKE_CFLAGS_HIDESYMS -fvisibility-inlines-hidden

DEFINES += QT_LINUXBASE  
FRSCSDK_DIR = $$(FRSCSDK_DIR)
isEmpty(FRSCSDK_DIR) {
    error("FRSCSDK_DIR environment variable not set")
}

# modifications to g++.conf
QMAKE_CC                = $${FRSCSDK_DIR}/bin/arm-obreey-linux-gnueabi-clang
QMAKE_CXX               = $${FRSCSDK_DIR}/bin/arm-obreey-linux-gnueabi-clang++
QMAKE_LINK              = $${FRSCSDK_DIR}/bin/arm-obreey-linux-gnueabi-clang++
QMAKE_LINK_SHLIB        = $${FRSCSDK_DIR}/bin/arm-obreey-linux-gnueabi-clang++
QMAKE_LIBS		= -lrt -lm

# modifications to linux.conf
QMAKE_AR                = $${FRSCSDK_DIR}/bin/arm-obreey-linux-gnueabi-ar cqs
QMAKE_OBJCOPY           = $${FRSCSDK_DIR}/bin/arm-obreey-linux-gnueabi-objcopy
QMAKE_NM                = $${FRSCSDK_DIR}/bin/arm-obreey-linux-gnueabi-nm -P
QMAKE_STRIP             = $${FRSCSDK_DIR}/bin/arm-obreey-linux-gnueabi-strip
QMAKE_AR_LTCG           = $${FRSCSDK_DIR}/arm-obreey-linux-gnueabi/bin/llvm-ar cqs
QMAKE_NM_LTCG           = $${FRSCSDK_DIR}/arm-obreey-linux-gnueabi/bin/llvm-nm -P
QMAKE_PKG_CONFIG 	= $${FRSCSDK_DIR}/bin/pkg-config

QMAKE_CFLAGS_ISYSTEM    =
QMAKE_LFLAGS_SHLIB     += -shared

load(qt_config)
